buildscript {
    ext {
        queryDslVersion = "5.0.0"
        testcontainersVersion = "1.17.6"
        snippetsDir = file('build/generated-snippets')
        restdocsVersion = '3.0.0'
        epagesRestdocsVersion = '0.16.2'
        springdocOpenApiVersion = '2.0.2'
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "3.0.0"
    id "io.spring.dependency-management" version "1.1.0"
    id "idea"
    id "org.asciidoctor.jvm.convert" version "3.3.2"

    // com.epages.restdocs-api-spec 의 spring boot 3.x 지원 업데이트에 맞추어 추후 적용 예정
//    id 'com.epages.restdocs-api-spec' version "0.16.2"
}

repositories {
    mavenCentral()
}

group = "com.dptablo"
version = "1.1.1"

sourceCompatibility = 17
targetCompatibility = 17

/** Spring RestDocs */
configurations {
    asciidoctorExtensions
}

dependencies {
    /* spring */
    implementation ("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'spring-boot-starter-tomcat'
    }

    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation 'org.springframework.boot:spring-boot-starter-undertow'

    implementation 'org.springframework.kafka:spring-kafka'

    /* database */
    implementation 'org.flywaydb:flyway-core:9.8.2'
    implementation "org.mariadb.jdbc:mariadb-java-client:3.1.0"
    implementation "org.postgresql:postgresql:42.5.1"

    /* data - jpa */
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    /* data - queryDSL */
    implementation "com.querydsl:querydsl-jpa:$queryDslVersion:jakarta"
    implementation "com.querydsl:querydsl-apt:$queryDslVersion:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    /* data - mongodb reactive */
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    /* data - r2dbc */
    implementation "org.springframework.data:spring-data-r2dbc"
    implementation 'io.r2dbc:r2dbc-pool:1.0.0.RELEASE'
    implementation 'org.postgresql:r2dbc-postgresql:1.0.0.RELEASE'

    /* jwt */
    implementation 'com.auth0:java-jwt:4.2.1'

    /* utils */
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    /* dev */
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    /* testing */
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.projectreactor:reactor-test:3.5.1'

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.1"
    testImplementation "org.assertj:assertj-core:3.23.1"

    testImplementation "org.testcontainers:junit-jupiter:$testcontainersVersion"

    /* testing - database */
    testImplementation "com.h2database:h2:2.1.214"
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:postgresql:$testcontainersVersion"
    testImplementation "org.testcontainers:mongodb:$testcontainersVersion"
    testImplementation "org.testcontainers:r2dbc:$testcontainersVersion"

    /* testing - mocking api server */
    testImplementation "com.squareup.okhttp3:okhttp:4.10.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"

    /* testing - spring kafka */
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation "org.testcontainers:kafka:$testcontainersVersion"

    /* Spring RestDocs */
    asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor:$restdocsVersion"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:$restdocsVersion"
    testImplementation "org.springframework.restdocs:spring-restdocs-webtestclient:$restdocsVersion"

    /* SpringDoc OpenAPI */
    implementation "org.springdoc:springdoc-openapi-starter-common:$springdocOpenApiVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocOpenApiVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:$springdocOpenApiVersion"

    /* com.epages restdocs-api-spec */
    // com.epages.restdocs-api-spec 의 spring boot 3.x 지원 업데이트에 맞추어 추후 적용 예정
//    implementation "com.epages:restdocs-api-spec:$epagesRestdocsVersion"
//    testImplementation "com.epages:restdocs-api-spec-mockmvc:$epagesRestdocsVersion"
//    testImplementation "com.epages:restdocs-api-spec-webtestclient:$epagesRestdocsVersion"
}

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks {
    test {
        useJUnitPlatform()
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

clean {
    delete 'src/main/generated'
    delete 'src/test/generated_tests'
    delete 'out'
}

/* Spring RestDocs */
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    doFirst {
        delete file("src/main/resources/static/docs")
    }

    dependsOn test
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    outputDir "${asciidoctor.outputDir}/html5"
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into "static/docs"
    }
}

task copyDocumentToStatic(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc/html5")
    into file("src/main/resources/static/docs")
}

task copyDocumentToJar(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("BOOT-INF/classes/static/docs")
}

build {
    dependsOn copyDocumentToStatic
    dependsOn copyDocumentToJar
}

// com.epages.restdocs-api-spec 의 spring boot 3.x 지원 업데이트에 맞추어 추후 적용 예정
//openapi {
//    host = 'localhost:9090'
//    basePath = '/api/v3'
//    schemes = ['http']
//    format = 'yaml'
//    title = 'My API'
//    version = '1.0.0'
//    description = 'My API description'
//}
//
//openapi3 {
//    setServer("http://localhost:9090")
//    title = "restdocs-swagger API Documentation"
//    description = "Spring REST Docs with SwaggerUI."
//    version = "1.0.0"
//    format = "yaml"
//}