buildscript {
    ext {
        queryDslVersion = "5.0.0"
        snippetsDir = file('build/generated-snippets')
        restdocsVersion = '2.0.6.RELEASE'
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "3.0.0"
    id "io.spring.dependency-management" version "1.1.0"
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id "idea"
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "com.dptablo"
version = "1.0.0"

sourceCompatibility = 17
targetCompatibility = 17

/** Spring RestDocs */
configurations {
    asciidoctorExtensions
}

dependencies {
    /* spring */
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.data:spring-data-r2dbc"

    /* queryDSL */
    implementation "com.querydsl:querydsl-jpa:$queryDslVersion:jakarta"
    implementation "com.querydsl:querydsl-apt:$queryDslVersion:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    /* database */
    implementation "org.mariadb.jdbc:mariadb-java-client:3.0.7"
    implementation "org.postgresql:postgresql:42.4.2"
    implementation 'org.postgresql:r2dbc-postgresql:1.0.0.RELEASE'
    implementation 'io.r2dbc:r2dbc-pool:1.0.0.RELEASE'
    implementation 'org.flywaydb:flyway-core:9.8.2'

    /* jwt */
    implementation 'com.auth0:java-jwt:4.2.1'

    /* utils */
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    /* dev */
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    /* testing */
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.springframework.security:spring-security-test'
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.1"
    testImplementation "org.assertj:assertj-core:3.23.1"

    /* testing - database */
    testImplementation "com.h2database:h2:2.1.214"
    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
    testImplementation 'org.testcontainers:postgresql:1.17.6'
    testImplementation 'org.testcontainers:r2dbc:1.17.6'

    /* testing - mocking api server */
    testImplementation "com.squareup.okhttp3:okhttp:4.10.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"

    /* Spring RestDocs */
    asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor:$restdocsVersion"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
}

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks {
    test {
        useJUnitPlatform()
    }

    clean {
        doFirst {
            delete "$projectDir/src/main/generated"
        }
    }
}

/* queryDSL */
def queryDslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    library = "com.querydsl:querydsl-apt:${queryDslVersion}"
    querydslSourcesDir = queryDslDir
}

sourceSets {
    main.java.srcDir queryDslDir
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

/* Spring RestDocs */
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    doFirst {
        delete file("src/main/resources/static/docs")
    }

    dependsOn test
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    outputDir "${asciidoctor.outputDir}/html5"
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into "static/docs"
    }
}

task copyDocumentToStatic(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc/html5")
    into file("src/main/resources/static/docs")
}

task copyDocumentToJar(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("BOOT-INF/classes/static/docs")
}

build {
    dependsOn copyDocumentToStatic
    dependsOn copyDocumentToJar
}